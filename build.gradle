
plugins {
    id 'java-library'
    id 'pl.allegro.tech.build.axion-release' version '1.10.3'
    id "io.codearte.nexus-staging" version "0.21.2"
    id "com.bmuschko.nexus" version "2.3.1" apply false
}

scmVersion {
    tag {
        prefix = 'smartparam'
    }
    versionCreator 'versionWithBranch'
}

allprojects {
    apply plugin: 'java-library'

    group = 'org.smartparam'
    version = scmVersion.version

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    ext.versions = [
            'polyjdbc'   : '0.2.2',
            'commonsLang': '2.6'
    ]

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
        compile group: 'org.picocontainer', name: 'picocontainer', version: '2.14.3'

        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile group: 'org.testng', name: 'testng', version: '6.8.1'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.0'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '1.5.0'
        testCompile group: 'com.googlecode.catch-exception', name: 'catch-exception', version: '1.2.0'

        testCompile group: 'ch.qos.logback', name: 'logback-core', version: '1.0.13'
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'
    }

    test {
        useTestNG()

        test.testLogging {
            exceptionFormat 'full'
        }
    }
}

subprojects {
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'jacoco'

    modifyPom {
        project {
            name project.name
            description 'Performant and lightweight Java library for handling application parametrization and decision making.'
            url 'http://smartparam.org'
            inceptionYear '2012'

            scm {
                url 'https://github.com/smartparam/smartparam'
                connection 'scm:git@github.com:smartparam/smartparam.git'
                developerConnection 'scm:git@github.com:smartparam/smartparam.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'przemek.hertel'
                    name 'Przemek Hertel'
                }
                developer {
                    id 'adam.dubiel'
                    name 'Adam Dubiel'
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = 'org.smartparam'

    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}